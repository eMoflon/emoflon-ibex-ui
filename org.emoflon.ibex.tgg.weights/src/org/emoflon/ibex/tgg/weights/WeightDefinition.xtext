grammar org.emoflon.ibex.tgg.weights.WeightDefinition with org.eclipse.xtext.xbase.Xbase

import "platform:/plugin/org.emoflon.ibex.tgg.core.language/model/Language.ecore#//csp/definition" as definition
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import 'http://www.eclipse.org/xtext/xbase/Xbase' as xbase
import "platform:/plugin/org.emoflon.ibex.tgg.core.language/model/Language.ecore" as language
import "platform:/plugin/org.emoflon.ibex.tgg.core.language/model/Language.ecore#//basic" as basic
import "platform:/plugin/org.emoflon.ibex.tgg.core.language/model/Language.ecore#//inplaceAttributes" as inplaceAttributes
import "platform:/plugin/org.emoflon.ibex.tgg.core.language/model/Language.ecore#//csp" as csp

generate weightDefinition "http://www.emoflon.org/ibex/tgg/weights/WeightDefinition"

WeightDefinitionFile:
	importedTgg = Import
	(
		weigthDefinitions+=RuleWeightDefinition
		|defaultCalc += DefaultCalculation
		|helperFuntions += HelperFuntion
		|variables += VariableDeclaration
	)*;

Import:
	'import' importURI=STRING
;

VariableDeclaration:
	'var' parameterType=JvmTypeReference name=ValidID
;

RuleWeightDefinition:
	{RuleWeightDefinition}
	'rule' rule = [language::TGGRule]
	weightCalc = XBlockExpression
	;

DefaultCalculation:
	{DefaultCalculation}
	'default'
	calc = XBlockExpression
;

HelperFuntion:
	{HelperFunction}
	'function' 
	returnType = JvmTypeReference
	name=ValidID
	'('
	(
		params += HelperFuncParameter
		(',' params += HelperFuncParameter)*
	)?
	')'
	body = XBlockExpression
;

HelperFuncParameter:
	parameterType=JvmTypeReference name=ValidID
;

