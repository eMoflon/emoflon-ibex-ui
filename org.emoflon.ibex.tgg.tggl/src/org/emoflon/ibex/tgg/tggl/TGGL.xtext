grammar org.emoflon.ibex.tgg.tggl.TGGL with org.emoflon.ibex.common.slimgt.SlimGT

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.emoflon.org/ibex/common/slimgt/SlimGT" as slimGT

generate tGGL "http://www.emoflon.org/ibex/tgg/tggl/TGGL"

@Override
EditorFile:
	imports += Import*
	(schema = Schema)?
  	rules += TGGRule*
  	(library = AttributeConditionDefinitionLibrary)?
;

Using:
	'using' importedNamespace = QualifiedNameWithWildcard
;

QualifiedNameWithWildcard:
	QualifiedName '.*'
;

/* ---------- */
/*   SCHEMA   */
/* ---------- */

Schema:
	'schema' name = QualifiedName
	'{'	
		('source' '{'
			sourceTypes += [ecore::EObject|ID]*
		'}')? 
		
		('target' '{'
			targetTypes += [ecore::EObject|ID]*
		'}')? 
	
		('correspondence' '{'
			correspondenceTypes += CorrespondenceType*
		'}')?	
		
		('attributeConditions' '{'
			attributeCondDefs += AttributeConditionDefinition*
		'}')?
	'}' 
;

AttributeConditionDefinition:
	name = ID '(' params += SlimParameter (',' params += SlimParameter)* ')' 
	'{'
		'sync' 
		'{'
			allowedSyncBindings += Binding (',' allowedSyncBindings += Binding)*
		'}'
		('gen'
		'{'
			allowedGenBindings += Binding (',' allowedGenBindings += Binding )*
		'}'
		)?
	'}'
;

Binding:
	'[' value += ('B'|'F')+ ']'
;

AttributeConditionDefinitionLibrary:
	'library' name = QualifiedName
	'{'
		(attributeCondDefs += AttributeConditionDefinition)*
	'}'
;

CorrespondenceType:
	name = ID ('extends' super=[CorrespondenceType])? ('{'
		'src->' source = [ecore::EClass | QualifiedName]  
		'trg->' target = [ecore::EClass | QualifiedName]
	'}')
;

AttributeCondition:
	name = [AttributeConditionDefinition] 
	'(' values += (LocalVariable | ExpressionOperand) 
		(',' values += (LocalVariable | ExpressionOperand))* ')'
;

@Override
ExpressionOperand:
	{ExpressionOperand} operand = (NodeAttributeExpression | ArithmeticLiteral | EnumExpression | Constant)
;

LocalVariable:
	name = ID
;

TGGLRuleRefinement:
	TGGLRuleRefinementPlain | TGGLRuleRefinementAliased
;

TGGLRuleRefinementPlain returns TGGLRuleRefinement : {TGGLRuleRefinementPlain}
	superRule=[TGGRule|ID]
;

TGGLRuleRefinementAliased returns TGGLRuleRefinement : {TGGLRuleRefinementAliased}
	superRule=[TGGRule|ID] 'as' name=ID
;


TGGRuleRefinementNode: 
	node=[SlimRuleNode|QualifiedName]
;

TGGLRuleRefinementCorrespondence: 
	refinement=[ecore::EObject|ID] '.' node=[CorrespondenceNode|ID]
;

/* ------------- */
/*   TGG RULES   */
/* ------------- */

@Override
SlimRuleNodeContext: {SlimRuleNodeContext}
	('@refines' refinement=TGGRuleRefinementNode WS)?
	'[=]' (local?='local')? context = SlimRuleNode
;

@Override
SlimRuleNodeCreation: {SlimRuleNodeCreation}
	('@refines' refinement=TGGRuleRefinementNode)?
	'[+]' creation = SlimRuleNode
;

@Override
SlimRuleNode: {SlimRuleNode}
	name = ID ':' type=[ecore::EClass | QualifiedName] ('{'
		(contextEdges += SlimRuleEdgeContext |
		createdEdges += SlimRuleEdgeCreation |
		assignments += SlimRuleAttributeAssignment)*
	'}')?
;

TGGRule:
	(abstract ?= 'abstract')? 
	'rule' name = ID 
	('refines' refinements += TGGLRuleRefinement (',' refinements += TGGLRuleRefinement)*)? 
	'{'
		('source' '{'
			sourceRule = SlimRule 
		'}')? 	
		
		('target' '{'
			targetRule = SlimRule 
		'}')? 
		
		('correspondence' '{'
			(correspondenceNodes += CorrespondenceNode)*	
		'}')? 
	
		('attributeConditions' '{'
			(attrConditions += AttributeCondition)*
		'}')? 
	'}'
;

CorrespondenceNode:
	(refinement=TGGLRuleRefinementCorrespondence)?
	(created ?= '++')? name = ID ':' type = [CorrespondenceType | QualifiedName] '{'
		'src->' source = [SlimRuleNode|ID]
		'trg->' target = [SlimRuleNode|ID]
	'}' 
;