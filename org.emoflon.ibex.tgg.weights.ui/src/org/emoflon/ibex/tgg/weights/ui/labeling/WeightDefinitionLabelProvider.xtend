/*
 * generated by Xtext 2.14.0
 */
package org.emoflon.ibex.tgg.weights.ui.labeling

import com.google.inject.Inject
import org.eclipse.emf.edit.ui.provider.AdapterFactoryLabelProvider
import org.emoflon.ibex.tgg.weights.weightDefinition.WeightDefinitionFile
import org.eclipse.xtext.xbase.ui.labeling.XbaseLabelProvider
import org.emoflon.ibex.tgg.weights.weightDefinition.RuleWeightDefinition
import org.emoflon.ibex.tgg.weights.weightDefinition.DefaultCalculation
import org.emoflon.ibex.tgg.weights.weightDefinition.HelperFunction
import org.emoflon.ibex.tgg.weights.weightDefinition.Import
import org.eclipse.emf.common.util.URI
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.emf.ecore.resource.impl.ResourceSetImpl
import org.eclipse.emf.ecore.resource.ContentHandler
import org.eclipse.emf.ecore.util.EcoreUtil
import language.TGG
import org.emoflon.ibex.tgg.weights.weightDefinition.VariableDeclaration

/**
 * Provides labels for EObjects.
 * 
 * See https://www.eclipse.org/Xtext/documentation/304_ide_concepts.html#label-provider
 */
class WeightDefinitionLabelProvider extends XbaseLabelProvider {

	/**
	 * Cached imported resource
	 */
	var Resource importedTGG

	@Inject
	new(AdapterFactoryLabelProvider delegate) {
		super(delegate);
	}

	def text(WeightDefinitionFile file) {
		val fileName = file.eResource.URI.lastSegment
		fileName.substring(0, fileName.length - 5)
	}

	def text(RuleWeightDefinition ruleElement) {
		ruleElement.rule?.name
	}

	def text(DefaultCalculation defaultCalc) {
		"Default Weight Calculation"
	}

	def String text(VariableDeclaration variable) {
		'''«variable.name» : «variable.parameterType.type.simpleName»'''
	}

	def text(Import importNode) {
		try {
			val importUri = importNode.importURI
			val uri = URI.createURI(importUri);
			val resolvedUri = uri.resolve(URI.createPlatformResourceURI("/", true))
			if ((importedTGG === null) || (importedTGG.URI != resolvedUri)) {
				importedTGG = new ResourceSetImpl().createResource(resolvedUri,
					ContentHandler.UNSPECIFIED_CONTENT_TYPE);
				importedTGG.load(null);
				EcoreUtil.resolveAll(importedTGG);
			}
			val tgg = importedTGG.contents.filter[it instanceof TGG].get(0) as TGG
			return '''TGG «tgg.name» ([«tgg.src.map[name].join(",")»]<=>[«tgg.trg.map[name].join(",")»])'''
		} catch (Exception e) {
			return '''TGG (unresolvable, at «importNode.importURI»)'''
		}
	}

	def String text(HelperFunction helperFunc) {
		'''«helperFunc.name»(«helperFunc.params.map[parameterType.type.simpleName].join(", ")») : «helperFunc.returnType.type.simpleName»'''
	}
}
